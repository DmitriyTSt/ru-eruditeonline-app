plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: "${project.rootDir}/config/android_common.gradle"
apply from: "${project.rootDir}/config/quality/quality.gradle"

android {
    namespace = "ru.eruditeonline.app"

    defaultConfig {
        applicationId "ru.eruditeonline.app"
        versionCode this.getVersionCode()
        versionName "1.0.0"

        vectorDrawables.useSupportLibrary = true
    }

    //для подписки релизного билда
    File releaseSignFile = rootProject.file('release_keystore.properties')
    Properties releaseSignProperties
    if (releaseSignFile.exists()) {
        releaseSignProperties = new Properties()
        releaseSignProperties.load(new FileInputStream(releaseSignFile))
    } else {
        // fake keystore properties file
        releaseSignProperties = new Properties()
    }

    signingConfigs {
        release {
            storeFile file('keystore/' + releaseSignProperties['keystore'])
            storePassword releaseSignProperties['storePassword']
            keyAlias releaseSignProperties['keyAlias']
            keyPassword releaseSignProperties['keyPassword']
        }
        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'AndroidDebugKey'
            storePassword 'android'
            keyPassword 'android'
        }
    }

    buildTypes {
        // release build for google play, master/release branches
        release {
            signingConfig signingConfigs.release
        }

        // debug build for developers only
        debug {
            applicationIdSuffix ".develop"
            signingConfig signingConfigs.debug
        }

        // internal build for testers
        internal {
            initWith release
            applicationIdSuffix ".develop"
        }
    }

    buildFeatures {
        buildConfig = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def appName = 'eruditeonline'
            def buildType = variant.buildType.name
            def newName = "${appName}-${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildType}"
            outputFileName = newName.toLowerCase() + ".apk"
        }
    }
}

def getVersionCode() {
    try {
        def versionCode = "git rev-list --count HEAD".execute([], project.rootProject.projectDir).text.trim()
        def result = Integer.parseInt(versionCode)
        return result
    }
    catch (ignored) {
        println "Error getting version code " + ignored.getLocalizedMessage()
        return -1
    }
}

dependencies {
    implementation(project(":common:core"))
    implementation(project(":common:serializer"))
    implementation(project(":common:coroutine"))
    implementation(project(":common:injectviewmodel"))
    implementation(project(":common:usecase"))
    implementation(project(":common:network"))

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.fragment.ktx)
    implementation(libs.androidx.viewpager2)
    implementation(libs.flexbox)
    implementation(libs.androidx.paging)
    implementation(libs.androidx.swiperefreshlayout)
    implementation(libs.androidx.splashscreen)

    implementation(libs.glide.core)
    implementation(libs.glide.compiler)

    implementation(libs.viewbinding.delegate)

    implementation(libs.coroutines.core)
    implementation(libs.coroutines.android)
    implementation(libs.androidx.livedata)
    implementation(libs.androidx.lifecycle.process)

    implementation(libs.bundles.retrofit)
    implementation(libs.gson)

    implementation(libs.androidx.navigation.fragment)
    implementation(libs.androidx.navigation.ui)

    implementation(libs.bundles.dagger.core)
    kapt(libs.bundles.dagger.kapt)

    implementation(libs.timber)
    implementation(libs.chucker)

    implementation(libs.androidx.browser)
    implementation(libs.androidx.security.crypto)

    implementation platform(libs.firebase.bom)
    implementation(libs.firebase.analytics)
    implementation(libs.firebase.crashlytics.asProvider())
}